pluginManagement {
	repositories {
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'https://repo.spring.io/snapshot' }
		gradlePluginPortal()
	}
}


rootProject.name = 'camanchaca'

String camanchacaFolderApps = 'largo_plazo'


//Add generic name to all modules
FileTree buildFiles = fileTree(rootDir) {
	List excludes = gradle.startParameter.projectProperties.get("excludeProjects")?.split(",")
	include '**/*.gradle'
	exclude 'main.gradle','build', '**/gradle', 'settings.gradle', 'buildSrc', '/build.gradle', '.*', 'out'
	if(excludes) {
		exclude excludes
	}
}

buildFiles.each { File buildFile ->

	boolean isDefaultName = 'build.gradle'.equals(buildFile.name)
	if(isDefaultName){
		String projectName = buildFile.parentFile.name
		String projectPath = ':' + projectName
		include projectPath
		def project = findProject("${projectPath}")
    if(buildFile.parentFile.parentFile.name.equals(camanchacaFolderApps)) {
			project.name = buildFile.parentFile.parentFile.name + '-' + projectName
		}else{
			project.name = rootProject.name + '-' + projectName
		}
		project.projectDir = buildFile.parentFile
		project.buildFileName = buildFile.name

	} else {

		String projectName = buildFile.name.replace('.gradle', '')
		String projectPath = ':' + projectName
		include projectPath
		def project = findProject("${projectPath}")
		project.name = projectName
		project.projectDir = buildFile.parentFile
		project.buildFileName = buildFile.name

	}
}

